#!/bin/csh -fe
####
#
#############################################################################
#                                                                           #
# Title: Init Files and Parameters                                          #
#                                                                           #
# (C) 2dx.org, GNU Plublic License.                                         #
#                                                                           #
# Created..........: 03/21/2011                                             #
# Last Modification: 03/21/2011                                             #
# Author...........: 2dx.org                                                #
#                                                                           #
#############################################################################
#
# SORTORDER: 2
#
# MANUAL: <B>Welcome to 2dx_image</B>
#
# MANUAL: This program assists you in the processing of one 2D crystal image. Help is available <A HREF="http://2dx.org/documentation/2dx-software">here</A>. Try the right mouse click on the various fields or variable names, to get more information. See also the introductory <A HREF="http://2dx.org/documentation/2dx-software/tutorial"><B>Movie</B></A> tutorials.
#
# MANUAL: 2dx_image uses external programs to display certain files. These can be defined in the 2dx configuration file, which you can find (and edit) in your home directory under .2dx/2dx.cfg . Use this file to define your prefered editor, web browser, etc..
#
# MANUAL: Edit the parameters in this central <I>Processing Data</I> pane. By default, only the most important parameters are accessible here. If you want to change one of the <I>advanced</I> parameters too, you can switch the display mode of this <I>Processing Data</I> pane at the left end of the top bar from "Simplified" to "Full Parameter List".
#
# MANUAL: 2dx tries to calculate or predict values for all parameters, based on available information. In several cases, you need to manually verify these values. These are then unerlaid with a blue stripe-pattern, until you have edited or confirmed manually the values.   
#
# MANUAL: After having edited the parameters, choose a script from the <I>Standard Scripts</I> and execute it via the <I>Start</I> button above.
#
# MANUAL: Upon first launch of this program, a default database in form of the file 2dx_image.cfg is created in the chosen  directory. The default entries for this database file are taken from the ${appDir_2dx}2dx/config/2dx_image.cfg file. These default values are then updated with values from the file ../2dx_image.cfg (the database file one level up from the current directory). Finally, this script 2dx_initialize.script is executed, and the resulting databse saved into the local directory's 2dx_image.cfg file.
#
# MANUAL: <HR>
#
# MANUAL: <IMG SRC="${appDir_2dx}/../config/2dx_image-algo-1.jpg">
#
# MANUAL: <HR>
#
# MANUAL: <IMG SRC="${appDir_2dx}/../config/2dx_image-algo-2.jpg">
#
# DISPLAY: imageorigin
# DISPLAY: imagename
# DISPLAY: nonmaskimagename
# DISPLAY: imagenumber
# DISPLAY: magnification
# DISPLAY: stepdigitizer
# DISPLAY: Calc_from_sample_pixel
# DISPLAY: sample_pixel
# DISPLAY: realcell
# DISPLAY: realang
# DISPLAY: df_min
# DISPLAY: df_searchrange1
# DISPLAY: RESMAX
# DISPLAY: CS
# DISPLAY: KV
# DISPLAY: tempkeep
# DISPLAY: initialization_executable
# DISPLAY: initialization_corr_endian
# DISPLAY: det_defocus
# DISPLAY: det_tilt
# DISPLAY: ctf_ttf
# DISPLAY: crop
# DISPLAY: comment
# DISPLAY: imagesidelength
#
#$end_local_vars
#
# Static directory variables at disposition are:
# appDir_2dx
# scripts-standard_2dx
# scripts-custom_2dx
#
set bin_2dx = ""
set proc_2dx = ""
set SCRATCH_DISK = ""
set FFTIR_DISK = ""
#
# Variables to be available from the config file are:
set imageorigin = ""
set tempkeep = ""
set imagename = ""
set nonmaskimagename = ""
set imagenumber = ""
set imagesidelength = ""
set realcell = ""
set magnification = ""
set stepdigitizer = ""
set Calc_from_sample_pixel = ""
set sample_pixel = ""
set realang = ""
set phacon = ""
set RESMIN = ""
set RESMAX = ""
set RADLIM = ""
set det_tilt = ""
set ctf_ttf = ""
set boxa1 = ""
set boxa2 = ""
set boxb1 = ""
set boxb2 = ""
set refori = ""
set quadrada = ""
set quadradb = ""
set initialization_executable = ""
set initialization_corr_endian = ""
set crop = ""
set MergeIWF = ""
set MergeIWP = ""
#
#$end_vars
#
set scriptname = 2dx_initialize_files
\rm -f LOGS/${scriptname}.results
#
set ccp4_setup = 'y'
source ${proc_2dx}/initialize
#
source ${proc_2dx}/2dx_makedirs
#
echo "<<@evaluate>>"
echo "<<@progress: 1>>"
#
if ( ! -e ${imagename}.mrc ) then
  set initialization_executable = 'y'
endif
#
if ( ${MergeIWF}x == "-1x" ) then
  echo "set MergeIWF = 0" >> LOGS/${scriptname}.results
endif
#
if ( ${MergeIWP}x == "-1x" ) then
  echo "set MergeIWP = 1" >> LOGS/${scriptname}.results
endif
#
set testname = `echo ${imagename} | cut -c2-`
set correct = 0
if (( ${nonmaskimagename} != ${imagename} ) && ( ${nonmaskimagename} != ${testname} )) then
  set correct = 1
endif
if ( ${nonmaskimagename} == "ImageName" ) then
  set correct = 1
endif
if ( ${correct} == '1' ) then
  set oldval = ${nonmaskimagename}
  set nonmaskimagename = ${imagename}
  #############################################################################
  ${proc_2dx}/linblock "Warning: correcting nonmaskimagename from ${oldval} to ${nonmaskimagename}"
  #############################################################################
  echo "set nonmaskimagename = ${nonmaskimagename}"  >> LOGS/${scriptname}.results
endif
# 
if ( ${imagenumber} == "ScriptWillPutNumberHere" ) then
  set imagenumber = `echo ${imagename} | ${bin_2dx}/2dx_getnumber.exe`
  #############################################################################
  ${proc_2dx}/linblock "Setting imagenumber to ${imagenumber}"
  #############################################################################
  echo "set imagenumber = ${imagenumber}"  >> LOGS/${scriptname}.results
endif
#
set new_mrc_created = 'n'
#
if ( ${initialization_executable} == "n" ) then
  #
  echo "Initialization not done. Date: ${date}" >> History.dat
  #
  ${bin_2dx}/2dx_endianTest.exe ${imagename}.mrc | head -n 2
  set correctend = `${bin_2dx}/2dx_endianTest.exe ${imagename}.mrc | tail -n 1`
  if ( ${correctend} == 'n' ) then
    if ( ${initialization_corr_endian} == "n" ) then
      echo ":: "
      ${proc_2dx}/linblock "#"
      ${proc_2dx}/linblock "#"
      ${proc_2dx}/linblock "WARNING: ${imagename}.mrc has the wrong endedness."
      echo ":: ${imagename}.mrc had wrong endedness" >> History.dat
      ${proc_2dx}/linblock "#"
      ${proc_2dx}/linblock "#"
      echo ":: "
      echo "::It is probably a file from a different computer with different architecture."
      echo "::You should uncheck initialization_executable and run this script again."
      echo ":: "
    else
      echo ":: "
      #############################################################################
      ${proc_2dx}/linblock "WARNING: wrong endedness. Tying 2dx_byteSwap.exe to correct endedness."
      #############################################################################
      \cp ${imagename}.mrc SCRATCH/TMPconverted.mrc
      ${bin_2dx}/2dx_byteSwap.exe SCRATCH/TMPconverted.mrc
      set correctend = `${bin_2dx}/2dx_endianTest.exe SCRATCH/TMPconverted.mrc | tail -n 1`
      if ( ${correctend} == 'y' ) then
        \mv SCRATCH/TMPconverted.mrc ${imagename}.mrc
      else
        \rm SCRATCH/TMPconverted.mrc
      endif
      #
      if ( ${correctend} == 'n' ) then
        #############################################################################
        ${proc_2dx}/linblock "WARNING: Still wrong endedness. Tying image_convert.exe to correct endedness."
        #############################################################################
        #
        ${bin_2dx}/image_convert.exe << eot
${imagename}.mrc
SCRATCH/TMPconverted.mrc
eot
        echo "<<@progress: 30>>"
        #
        if ( ! -e SCRATCH/TMPconverted.mrc ) then
          #############################################################################
          ${proc_2dx}/linblock "image_convert.exe FAILED."
          ${proc_2dx}/linblock "image_convert.exe FAILED." >> History.dat
          ${proc_2dx}/linblock "Attempting to convert with byte_swap_map.exe."
          #############################################################################
          #
          ${bin_2dx}/byte_swap_map.exe << eot
${imagename}.mrc
eot
          #
          setenv IN="${imagename}.mrc"
          \rm -f SCRATCH/TMP001.tmp
          ${bin_2dx}/header.exe | grep old\ style\ 20th > SCRATCH/TMP001.tmp
          \ls -l SCRATCH/TMP001.tmp
          if ( -s SCRATCH/TMP001.tmp ) then
            #############################################################################
            echo ":: byte_swap_map.exe successfully corrected the endianness, "
            echo ":: but ${imagename}.mrc is old style 20th century map."
            echo ":: Trying again image_convert.exe to update."
            #############################################################################
            #
            ${bin_2dx}/image_convert.exe << eot
${imagename}.mrc
SCRATCH/TMPconverted.mrc
eot
            #
            if ( -e SCRATCH/TMPconverted.mrc ) then
              \mv -f SCRATCH/TMPconverted.mrc ${imagename}.mrc
              echo ":: That worked. Image should be good now."
              echo "#WARNING: File format and endedness of the input file corrected."  >> LOGS/${scriptname}.results
            else
              echo ":: image_convert.exe failed again."
              echo "#WARNING: WARNING: All attempts to correct endianness failed. Some problem with the file."  >> LOGS/${scriptname}.results
            endif
          else
            echo ":: byte_swap_map failed."
            echo "#WARNING: WARNING: All attempts to correct endianness failed. Some problem with the file."  >> LOGS/${scriptname}.results
          endif
          ${proc_2dx}/linblock "Finished Endian Test"
        else
          \mv -f ${imagename}.mrc ${imagename}-wrong-endedness.mrc
          \mv -f SCRATCH/TMPconverted.mrc ${imagename}.mrc
          ${proc_2dx}/linblock "WARNING: Endedness of the input file was corrected."
          ${proc_2dx}/linblock "WARNING: Endedness of the input file was corrected." >> History.dat
          echo "# WARNING: Warning: Endedness of the input file was corrected."  >> LOGS/${scriptname}.results
        endif
      endif
      #
    endif
    #
  else
    #
    if ( ! -e ${imagename}.mrc ) then
      ${proc_2dx}/linblock "Image ${imagename}.mrc does not exist."
      #
      if ( ! ( -e ${imagename}.tif || -e ${imagename}.tiff ) ) then
        ${proc_2dx}/protest "Image ${imagename}.tif does not exist."
      else
        if ( -e ${imagename}.tiff ) then
          ${proc_2dx}/linblock "Renaming ${imagename}.tiff into ${imagename}.tif"
          ${proc_2dx}/linblock "Renaming ${imagename}.tiff into ${imagename}.tif" >> History.dat
          \mv -f ${imagename}.tiff ${imagename}.tif
        endif
        ${proc_2dx}/linblock "Image ${imagename}.tif found...converting."
        #
        ${bin_2dx}/tif2mrc.exe << eot
${imagename}.tif
${imagename}.mrc
y
eot
        #
        set new_mrc_created = 'y'
        #
        echo "# IMAGE-IMPORTANT: "${imagename}.mrc "<Image>" >> LOGS/${scriptname}.results    
      endif
    else
      echo "# IMAGE-IMPORTANT: "${imagename}.mrc "<Image>" >> LOGS/${scriptname}.results
    endif
    #
    #################################################################################
    echo ":: "
    ${proc_2dx}/linblock "Parameter initialization ran already."
    echo "::If this is wrong, set 'Initialization can be executed (again)' to 'Yes'."
    echo ":: "
    #################################################################################
  endif
  #
  if ( -e ${imagename}-orignal.mrc ) then
    echo "# IMAGE-IMPORTANT: ${imagename}-orignal.mrc <Original Image>" >> LOGS/${scriptname}.results
  endif
  if ( ${nonmaskimagename} != ${imagename} ) then
    echo "# IMAGE-IMPORTANT: ${imagename}.mrc <Image>" >> LOGS/${scriptname}.results
  endif
  echo "# IMAGE-IMPORTANT: ${nonmaskimagename}.mrc <Non-Masked Image>" >> LOGS/${scriptname}.results
  #
  if ( -e ${nonmaskimagename}-original-big-old.mrc ) then
    echo "# IMAGE: ${nonmaskimagename}-original-big-old.mrc <Non-Masked Original Big Old Image>"  >> LOGS/${scriptname}.results
  endif
  #
  if ( -e ${nonmaskimagename}-original-big.mrc ) then
    echo "# IMAGE: ${nonmaskimagename}-original-big.mrc <Non-Masked Original Big Image>"  >> LOGS/${scriptname}.results
  endif
  #
  if ( ${Calc_from_sample_pixel} == "y" ) then
    set magnification = `echo ${stepdigitizer} ${sample_pixel} | awk '{ s = $1 * 10000.0 / $2 } END { print s }'`
    echo "set magnification = ${magnification}" >> LOGS/${scriptname}.results
    ${proc_2dx}/linblock "Calculating magnification as ${magnification}."
  else
    set sample_pixel = `echo ${stepdigitizer} ${magnification} | awk '{ s = $1 * 10000.0 / $2 } END { print s }'`
    echo "set sample_pixel = ${sample_pixel}" >> LOGS/${scriptname}.results
    ${proc_2dx}/linblock "Calculating pixel size on sample level as ${sample_pixel} Angstroems."
  endif
  #
else
  #
  echo "Initialization starting. Date: ${date}" >> History.dat
  #
  #################################################################################
  #################################################################################
  ${proc_2dx}/lin "Running some tests..."
  #################################################################################
  #################################################################################
  #
  if ( ! -e APH/${imagename}.cor.aph ) then
    if ( ! -e ${imagename}.mrc ) then
    
      #############################################################################
      ${proc_2dx}/linblock "Image ${imagename}.mrc does not exist."
      #############################################################################
      #
      if ( ( ! -e ${imagename}.tif ) && ( -e ${imagename}.tiff ) ) then
        ${proc_2dx}/linblock "Renaming ${imagename}.tiff into ${imagename}.tif"
        \mv -f ${imagename}.tiff ${imagename}.tif
      endif 
      if ( -e ${imagename}.tif ) then
        ${proc_2dx}/linblock "Image ${imagename}.tif found...converting."
        #
        ${bin_2dx}/tif2mrc.exe << eot
${imagename}.tif
${imagename}.mrc
y
eot
        #
        set new_mrc_created = 'y'
        #
      else
        #############################################################################
        ${proc_2dx}/linblock "Image ${imagename}.tif does not exist."
        #############################################################################
        # 
        echo "dummy" > zzzzz27765.mrc
        set filename = `ls -1 *.mrc | sort | head -n 1`
        if ( ${filename} != "zzzzz27765.mrc" ) then
          set imagename = `echo ${filename} | cut -d\. -f1`
          if ( ! -e ${imagename}.mrc ) then
            ${proc_2dx}/linblock "Image ${imagename}.mrc not existing."
            ${proc_2dx}/linblock "You probably use more than one dot in the image name, which is not recommended."
            echo "#WARNING: Image ${imagename}.mrc not existing."  >> LOGS/${scriptname}.results
            echo "#WARNING: You probably use more than one dot in the image name, which is not recommended."  >> LOGS/${scriptname}.results
          endif
        else
          #
          #############################################################################
          ### Testing for presence of *.tif
          #############################################################################
          #
          echo "dummy" > zzzzz27765.tif
          set filename = `ls -1 *.tif | sort | head -n 1`
          if ( ${filename} != "zzzzz27765.tif" ) then
            set imagename = `echo ${filename} | cut -d\. -f1`
            if ( ! -e ${imagename}.tif ) then
              ${proc_2dx}/linblock "Image ${imagename}.tif not existing."
              ${proc_2dx}/linblock "You probably use more than one dot in the image name, which is not recommended."
              echo "#WARNING: Image ${imagename}.tif not existing."  >> LOGS/${scriptname}.results
              echo "#WARNING: You probably use more than one dot in the image name, which is not recommended."  >> LOGS/${scriptname}.results
            else
              ${proc_2dx}/linblock "Image ${imagename}.tif found...converting."
              #
              ${bin_2dx}/tif2mrc.exe << eot
${imagename}.tif
${imagename}.mrc
y
eot
              #
              set new_mrc_created = 'y'
              #
            endif
          endif
          \rm -f zzzzz27765.tif
          #
          #############################################################################
          ### Testing for presence of *.tiff
          #############################################################################
          #
          echo "dummy" > zzzzz27765.tiff
          set filename = `ls -1 *.tiff | sort | head -n 1`
          if ( ${filename} != "zzzzz27765.tiff" ) then
            set imagename = `echo ${filename} | cut -d\. -f1`
            if ( ! -e ${imagename}.tiff ) then 
              ${proc_2dx}/linblock "Image ${imagename}.tiff not existing."
              ${proc_2dx}/linblock "You probably use more than one dot in the image name, which is not recommended."
              echo "#WARNING: Image ${imagename}.tiff not existing."  >> LOGS/${scriptname}.results
              echo "#WARNING: You probably use more than one dot in the image name, which is not recommended."  >> LOGS/${scriptname}.results
            else
              ${proc_2dx}/linblock "Image ${imagename}.tiff found...converting."
              #
              ${bin_2dx}/tif2mrc.exe << eot
${imagename}.tiff
${imagename}.mrc
y
eot
              #
              set new_mrc_created = 'y'
              #
            endif
          endif
          \rm -f zzzzz27765.tiff
        endif
        \rm -f zzzzz27765.mrc
      endif
      #
      echo "set imagename = ${imagename}"  >> LOGS/${scriptname}.results
      set nonmaskimagename = ${imagename}
      echo "set nonmaskimagename = ${nonmaskimagename}"  >> LOGS/${scriptname}.results
      #
    endif
    #
    if ( ! -e ${imagename}.mrc ) then
      echo ":: "
      echo ":: "
      echo "::     ERROR: ${imagename}.mrc not existing."
      echo ":: "
      echo "::     Use filename like PROT0012345600.tif or PROT0012345600.mrc"
      echo ":: "
      echo ":: "
      ${proc_2dx}/protest "ABORTING."
    endif
    #
    ${bin_2dx}/2dx_endianTest.exe ${imagename}.mrc | head -n 2
    #
    set correctend = `${bin_2dx}/2dx_endianTest.exe ${imagename}.mrc | tail -n 1`
    #
    if ( ${correctend} == 'n' ) then
      #############################################################################
      ${proc_2dx}/linblock "WARNING: wrong endedness. Tying 2dx_byteSwap.exe to correct endedness."
      ${proc_2dx}/linblock "WARNING: wrong endedness. Tying 2dx_byteSwap.exe to correct endedness." >> History.dat
      #############################################################################
      \cp ${imagename}.mrc SCRATCH/TMPconverted.mrc
      ${bin_2dx}/2dx_byteSwap.exe SCRATCH/TMPconverted.mrc
      set correctend = `${bin_2dx}/2dx_endianTest.exe SCRATCH/TMPconverted.mrc | tail -n 1`
      if ( ${correctend} == 'y' ) then
        \mv SCRATCH/TMPconverted.mrc ${imagename}.mrc
      else
        \rm SCRATCH/TMPconverted.mrc
      endif
    endif
    #
    if ( ${correctend} == 'n' ) then
      #############################################################################
      ${proc_2dx}/linblock "WARNING: Still wrong endedness. Tying image_convert.exe to correct endedness."
      ${proc_2dx}/linblock "WARNING: Still wrong endedness. Tying image_convert.exe to correct endedness." >> History.dat
      #############################################################################
      #
      ${bin_2dx}/image_convert.exe << eot
${imagename}.mrc
SCRATCH/TMPconverted.mrc
eot
      echo "<<@progress: 30>>"
      #
      if ( ! -e SCRATCH/TMPconverted.mrc ) then
        #############################################################################
        ${proc_2dx}/linblock "image_convert.exe FAILED."
        ${proc_2dx}/linblock "Attempting to convert with byte_swap_map.exe."
        ${proc_2dx}/linblock "Attempting to convert with byte_swap_map.exe." >> History.dat
        #############################################################################
        #
        ${bin_2dx}/byte_swap_map.exe << eot
${imagename}.mrc
eot
        #
        setenv IN="${imagename}.mrc"
        \rm -f SCRATCH/TMP001.tmp
        ${bin_2dx}/header.exe | grep old\ style\ 20th > SCRATCH/TMP001.tmp 
        \ls -l SCRATCH/TMP001.tmp 
        if ( -s SCRATCH/TMP001.tmp ) then
          #############################################################################
          echo ":: byte_swap_map.exe successfully corrected the endianness, "
          echo ":: but ${imagename}.mrc is old style 20th century map."
          echo ":: Trying again image_convert.exe to update."
          #############################################################################
          #
          ${bin_2dx}/image_convert.exe << eot
${imagename}.mrc
SCRATCH/TMPconverted.mrc
eot
          #
          if ( -e SCRATCH/TMPconverted.mrc ) then
            \mv -f SCRATCH/TMPconverted.mrc ${imagename}.mrc
            echo ":: That worked. Image should be good now."
            echo "#WARNING: File format and endedness of the input file corrected."  >> LOGS/${scriptname}.results
          else
            echo ":: image_convert.exe failed again."
            echo "#WARNING: WARNING: All attempts to correct endianness failed. Some problem with the file."  >> LOGS/${scriptname}.results
          endif
        else
          echo ":: byte_swap_map failed."
          echo "#WARNING: WARNING: All attempts to correct endianness failed. Some problem with the file."  >> LOGS/${scriptname}.results
        endif
        ${proc_2dx}/linblock "Finished Endian Test"
      else
        \mv -f ${imagename}.mrc ${imagename}-wrong-endedness.mrc
        \mv -f SCRATCH/TMPconverted.mrc ${imagename}.mrc
        ${proc_2dx}/linblock "WARNING: Endedness of the input file was corrected."
        ${proc_2dx}/linblock "WARNING: Endedness of the input file was corrected." >> History.dat
        echo "# WARNING: Warning: Endedness of the input file was corrected."  >> LOGS/${scriptname}.results
      endif
    endif
    #
    set testname = `echo ${imagename} | cut -c2-`
    if ((${nonmaskimagename} != ${imagename}) && (${nonmaskimagename} != ${testname})) then
      set oldval = ${nonmaskimagename}
      set nonmaskimagename = ${imagename}
      #############################################################################
      ${proc_2dx}/linblock "Warning: correcting nonmaskimagename from ${oldval} to ${nonmaskimagename}"
      #############################################################################
      echo "set nonmaskimagename = ${nonmaskimagename}"  >> LOGS/${scriptname}.results
    endif
    # 
    if ( ${imagenumber} == "ScriptWillPutNumberHere" || ${imagenumber} == "1000" ) then
      set imagenumber = `echo ${imagename} | ${bin_2dx}/2dx_getnumber.exe`
      #############################################################################
      ${proc_2dx}/linblock "Setting imagenumber to ${imagenumber}"
      #############################################################################
      echo "set imagenumber = ${imagenumber}"  >> LOGS/${scriptname}.results
    endif
    #
    echo "<<@progress: 40>>"
    setenv IN ${imagename}.mrc
    set dimens = `${bin_2dx}/header.exe | awk "/Number\ of\ columns/{print $1}" | cut -c51-`
    set sizeX = `echo ${dimens} | cut -d\  -f1` 
    set sizeY = `echo ${dimens} | cut -d\  -f2` 
    if ( ${sizeX} != ${sizeY} ) then
      ${proc_2dx}/linblock "ERROR: only square images are supported."
      echo "#WARNING: ERROR: only square images are supported."  >> LOGS/${scriptname}.results
      #
      if( ${crop} == "0" ) then
        #############################################################################
        ${proc_2dx}/linblock "Crop image into smaller square array"
        ${proc_2dx}/linblock "Crop image into smaller square array" >> History.dat
        #############################################################################
        #
        set newsize = `echo ${sizeX} ${sizeY} | awk '{ if ( $1 < $2 ) { s = $1 } else { s = $2 }} END { print s }'`
      else
        #############################################################################
        ${proc_2dx}/linblock "Pad image into larger square array"
        ${proc_2dx}/linblock "Pad image into larger square array" >> History.dat
        #############################################################################
        #
        set newsize = `echo ${sizeX} ${sizeY} | awk '{ if ( $1 > $2 ) { s = $1 } else { s = $2 }} END { print s }'`
      endif
      ${proc_2dx}/linblock "New image size will be ${newsize}"
      ${proc_2dx}/linblock "New image size will be ${newsize}" >> History.dat
      \rm -f SCRATCH/TMPnewsize.mrc
      #
      ${bin_2dx}/labelh.exe << eot
${imagename}.mrc
30
SCRATCH/TMPnewsize.mrc
${newsize}
eot
      #
        if ( ! -e ${imagename}-original.mrc ) then
        \mv -f ${imagename}.mrc ${imagename}-original.mrc
      else
        ${proc_2dx}/linblock "${imagename}-original.mrc already existing."
        ${proc_2dx}/protest "ERROR: Renaming not possible."
      endif
      \mv -f SCRATCH/TMPnewsize.mrc ${imagename}.mrc
      #
      echo "# IMAGE-IMPORTANT: ${imagename}-orignal.mrc <Original Image>"  >> LOGS/${scriptname}.results
      #
      set sizeX = ${newsize}
      #
      #############################################################################
      ${proc_2dx}/linblock "Testing new image size of ${sizeX} for prime factors"
      #############################################################################
      #
      set primesfile = SCRATCH/2dx_primes.dat  
      #
      \rm -f ${primesfile}}
      #
      ${bin_2dx}/2dx_primes.exe << eot
${sizeX}
${primesfile}
eot
      #
    else
      #############################################################################
      ${proc_2dx}/lin "Testing new image size of ${sizeX} for prime factors"
      #############################################################################
      #
      set primesfile = SCRATCH/2dx_primes.dat  
      #
      \rm -f ${primesfile}}
      #
      ${bin_2dx}/2dx_primes.exe << eot > SCRATCH/TMPprimesout.dat
${sizeX}
${primesfile}
eot
      #
      if ( -e ${primesfile} ) then
        cat SCRATCH/TMPprimesout.dat
      else
        cat SCRATCH/TMPprimesout.dat | sed 's/::/:/g'
      endif
      \rm -f SCRATCH/TMPprimesout.dat
      #
    endif
    #
    if ( -e ${primesfile} ) then
      set bettersize = `cat ${primesfile} | head -n 1`
      set cropdimensions = `cat ${primesfile} | head -n 2 | tail -n 1`
      #
      #############################################################################
      ${proc_2dx}/linblock "cutting image down into better smaller size of ${bettersize}"
      ${proc_2dx}/linblock "cutting image down into better smaller size of ${bettersize}" >> History.dat
      #############################################################################  
      #
      \rm -f SCRATCH/TMPnewsize1.mrc
      #
      ${bin_2dx}/labelh.exe << eot
${imagename}.mrc
1
SCRATCH/TMPnewsize1.mrc
${cropdimensions}
eot
      #
      ${proc_2dx}/linblock "Cropping done."
      #
      #############################################################################
      ${proc_2dx}/linblock "Pad into image with same dimensions to get the header right"
      #############################################################################
      #
      \rm -f SCRATCH/TMPnewsize2.mrc
      #
      ${bin_2dx}/labelh.exe << eot
SCRATCH/TMPnewsize1.mrc
30
SCRATCH/TMPnewsize2.mrc
${bettersize}
eot
      echo "<<@progress: 60>>"
      #
      ${proc_2dx}/lin "Padding done."
      #
     \mv -f SCRATCH/TMPnewsize2.mrc ${imagename}.mrc
      #
      set sizeX = ${bettersize}
      #
      \rm SCRATCH/TMPnewsize1.mrc
      \rm ${primesfile}
    endif
    #
    set newsize = `echo ${sizeX} | awk '{ if ( $1 < 1024 ) { s = 2 * $1 } else { s = $1 }} END { print s }'`
    #
    if ( ${newsize} != ${sizeX} ) then
      #
      #############################################################################
      echo ":: "
      echo "::WARNING: Interpolating image up to ${newsize}"
      echo ":: "
      echo "#WARNING: WARNING: Image was up-interpolated two times, to ${newsize} pixels" >> LOGS/${scriptname}.results
      #############################################################################  
      #
      \rm -f SCRATCH/TMPnewsize1.mrc
      #
      ${bin_2dx}/labelh.exe << eot
${imagename}.mrc
29
SCRATCH/TMPnewsize1.mrc
eot
      #
      ${proc_2dx}/linblock "Interpolation done."
      #
      echo "<<@progress: 62>>"
      #
      if ( ! -e ORIGINAL.mrc ) then
        \mv -f ${imagename}.mrc ORIGINAL.mrc
        echo "#IMAGE-IMPORTANT: ORIGINAL.mrc <Original image>" >> LOGS/${scriptname}.results
      endif
      #
      \mv -f SCRATCH/TMPnewsize1.mrc ${imagename}.mrc
      #
      set sizeX = ${newsize}
      #
    endif
    #
    if ( ${sizeX} != ${imagesidelength} ) then
      set oldval = ${imagesidelength}
      set imagesidelength = ${sizeX}
      ${proc_2dx}/linblock "WARNING: correcting imagesidelength from ${oldval} to ${imagesidelength}"
      echo "set imagesidelength = ${imagesidelength}"  >> LOGS/${scriptname}.results
    endif
    #
    ##################################################################
    #
  else
    if ( ! -e ${imagename}.mrc ) then
      ${proc_2dx}/linblock "Image ${imagename}.mrc does not exist."
      #
      if ( ( -e ${imagename}.tiff ) && ( ! -e ${imagename}.tif ) ) then
        ${proc_2dx}/linblock "Renaming ${imagename}.tiff into ${imagename}.tif"
        \mv -f ${imagename}.tiff ${imagename}.tif
      endif
      #
      if ( ! -e ${imagename}.tif ) then
        ${proc_2dx}/protest "Image ${imagename}.tif does not exist."
      else
        ${proc_2dx}/linblock "Image ${imagename}.tif found...converting."
        #
        ${bin_2dx}/tif2mrc.exe << eot
${imagename}.tif
${imagename}.mrc
y
eot
        #
        set new_mrc_created = 'y'
        #
        echo "# IMAGE-IMPORTANT: "${imagename}.mrc "<Image>" >> LOGS/${scriptname}.results    
      endif
    else
      echo "# IMAGE-IMPORTANT: "${imagename}.mrc "<Image>" >> LOGS/${scriptname}.results
    endif
    #
  endif
  #
  if ( ${Calc_from_sample_pixel} == "y" ) then
    set magnification = `echo ${stepdigitizer} ${sample_pixel} | awk '{ s = $1 * 10000.0 / $2 } END { print s }'`
    echo "set magnification = ${magnification}" >> LOGS/${scriptname}.results
    ${proc_2dx}/linblock "Calculating magnification as ${magnification}."
  else
    set sample_pixel = `echo ${stepdigitizer} ${magnification} | awk '{ s = $1 * 10000.0 / $2 } END { print s }'`
    echo "set sample_pixel = ${sample_pixel}" >> LOGS/${scriptname}.results
    ${proc_2dx}/linblock "Calculating pixel size on sample level as ${sample_pixel} Angstroems."
  endif
  #
  #
  if ( `echo ${phacon} | awk '{ if (( $1 < 0 ) || ( $1 > 1 )) { s = 1 } else { s = 0 }} END { print s }'` == 1 ) then
    set oldval = ${phacon}
    set phacon = 0.9975
    ${proc_2dx}/linblock "WARNING: correcting phacon from ${oldval} to ${phacon}"
    echo "set phacon = ${phacon}"  >> LOGS/${scriptname}.results
  endif
  #
  echo "<<@progress: 65>>"
  #
  if ( `echo ${RESMIN} ${RESMAX} | awk '{ if ( $1 < $2 ) { s = 1 } else { s = 0 }} END { print s }'` == 1 ) then
    set oldval = ${RESMIN}
    set RESMIN = ${RESMAX}
    set RESMAX = ${oldval}
    ${proc_2dx}/linblock "WARNING: exchanging RESMIN and RESMAX, to RESMIN=${RESMIN}, and RESMAX=${RESMAX}"
    echo "set RESMIN = ${RESMIN}"  >> LOGS/${scriptname}.results
    echo "set RESMAX = ${RESMAX}"  >> LOGS/${scriptname}.results
  endif
  #
  set testval1 = `echo ${RADLIM} | cut -d\, -f1`
  set testval2 = `echo ${RADLIM} | cut -d\, -f2`
  set testval3 = `echo ${RADLIM} | cut -d\, -f3`
  set correctit = 0
  #
  if ( `echo ${testval1} | awk '{ if (( $1 < 0 ) || ( $1 > 50 )) { s = 1 } else { s = 0 }} END { print s }'` == 1 ) then
    set oldval = ${testval1}
    set testval1 = 35
    set correctit = 1 
    ${proc_2dx}/linblock "WARNING: correcting RADLIM first value from ${oldval} to ${testval1}"
  endif
  #
  if ( `echo ${testval2} | awk '{ if (( $1 < 0 ) || ( $1 > 50 )) { s = 1 } else { s = 0 }} END { print s }'` == 1 ) then
    set oldval = ${testval2}
    set testval2 = 35
    set correctit = 1 
    ${proc_2dx}/linblock "WARNING: correcting RADLIM second value from ${oldval} to ${testval2}"
  endif
  #
  if ( ${correctit} == 1 ) then
    set RADLIM = `echo ${testval1},${testval2},${testval3}`
    echo "set RADLIM = ${RADLIM}"  >> LOGS/${scriptname}.results
  endif
  #
  set newimagenumber = `echo ${imagenumber} | ${bin_2dx}/2dx_getnumber.exe`
  if ( ${newimagenumber} != ${imagenumber} ) then
    ${proc_2dx}/linblock "WARNING: correcting imagenumber from ${imagenumber} to ${newimagenumber}"
    set imagenumber = ${newimagenumber}
    echo "set imagenumber = ${imagenumber}"  >> LOGS/${scriptname}.results
  endif
  #
  set testval = `echo ${imagenumber} | wc -c`
  if ( `echo ${testval} | awk '{ if ( $1 < 11 ) { s = 1 } else { s = 0 }} END { print s }'` == 1 ) then
    set oldval = ${imagenumber}
    set imagenumber = `echo 0000000000 | cut -c${testval}-`${imagenumber}
    ${proc_2dx}/linblock "WARNING: correcting imagenumber from ${oldval} to ${imagenumber}"
    echo "set imagenumber = ${imagenumber}"  >> LOGS/${scriptname}.results
  endif  
  #
  if ( `echo ${testval} | awk '{ if ( $1 > 11 ) { s = 1 } else { s = 0 }} END { print s }'` == 1 ) then
    set oldval = ${imagenumber}
    set startnum = `echo ${testval} | awk '{ s = $1 - 10 } END { print s }'`
    set endnum   = `echo ${testval} | awk '{ s = $1 - 1 } END { print s }'`
    set imagenumber = `echo ${imagenumber} | cut -c${startnum}-${endnum}`
    ${proc_2dx}/linblock "WARNING: correcting imagenumber from ${oldval} to ${imagenumber}"
    echo "set imagenumber = ${imagenumber}"  >> LOGS/${scriptname}.results
  endif  
  #
  # boxa1 and boxb1 are for the first UnbendI and following UnbendII reference diameters.
  #
  if ( ${boxa1} == "0" ) then
    set boxa1 = `echo ${imagesidelength} | awk '{ s = int( $1 / 10 ) } END { print s }'`
    echo "set boxa1 = ${boxa1}"  >> LOGS/${scriptname}.results
  endif
  #
  if ( ${boxb1} == "0" ) then
    set boxb1 = `echo ${imagesidelength} | awk '{ s = int( $1 / 16 ) } END { print s }'`
    echo "set boxb1 = ${boxb1}"  >> LOGS/${scriptname}.results
  endif
  #
  # boxa2 and boxb2 are for shifting SpotScan spots into their correct location. 
  # These references need to be much larger, optimally the diameter of one of the SpotScan spots.
  # This is only used if you have SpotScan spots (and set the corresponding flag to "y").
  #
  if ( ${boxa2} == "0" ) then
    set boxa2 = `echo ${imagesidelength} | awk '{ s = int( $1 / 4 )  } END { print s }'`
    echo "set boxa2 = ${boxa2}"  >> LOGS/${scriptname}.results
  endif
  #
  if ( ${boxb2} == "0" ) then
    set boxb2 = `echo ${imagesidelength} | awk '{ s = int( $1 / 4 )  } END { print s }'`
    echo "set boxb2 = ${boxb2}"  >> LOGS/${scriptname}.results
  endif
  #
  if ( ${refori} == "0,0" ) then
    set reforix = `echo ${imagesidelength} | awk '{ s = int( $1 / 2 ) } END { print s }'`
    set refori = ${reforix},${reforix}
    echo "set refori = ${refori}"  >> LOGS/${scriptname}.results
  endif
  #
  set quadraday = `echo ${quadrada}noval | cut -d\, -f2`
  if ( ${quadraday} == "noval" ) then
    set tmp = `echo ${quadrada} | cut -d\, -f1` 
    set quadrada = ${tmp},${tmp}
    ${proc_2dx}/linblock "WARNING: correcting quadrada to ${quadrada}"
    echo "set quadrada = ${quadrada}"  >> LOGS/${scriptname}.results
  endif
  #
  set quadradby = `echo ${quadradb}noval | cut -d\, -f2`
  if ( ${quadradby} == "noval" ) then
    set tmp = `echo ${quadradb} | cut -d\, -f1` 
    set quadradb = ${tmp},${tmp}
    ${proc_2dx}/linblock "WARNING: correcting quadradb to ${quadradb}"
    echo "set quadradb = ${quadradb}"  >> LOGS/${scriptname}.results
  endif
  #
  if ( ${nonmaskimagename} != ${imagename} ) then
    echo "# IMAGE-IMPORTANT: ${imagename}.mrc <Image>"  >> LOGS/${scriptname}.results
  endif
  echo "# IMAGE-IMPORTANT: ${nonmaskimagename}.mrc <Non-Masked Image>" >> LOGS/${scriptname}.results
  #
  if ( -e ${nonmaskimagename}-original-big-old.mrc ) then
    echo "# IMAGE: ${nonmaskimagename}-original-big-old.mrc <Non-Masked Original Big Old Image>"  >> LOGS/${scriptname}.results
  endif
  #
  if ( -e ${nonmaskimagename}-original-big.mrc ) then
    echo "# IMAGE: ${nonmaskimagename}-original-big.mrc <Non-Masked Original Big Image>"  >> LOGS/${scriptname}.results
  endif
  #
  if ( ${initialization_executable} == "y" ) then
    set initialization_executable = "n"
    echo "set initialization_executable = ${initialization_executable}"  >> LOGS/${scriptname}.results
  endif
  # 
endif
#
#
#
#############################################################################
#############################################################################
##### Now see, if new MRC was created, that needs to be adapted:
#############################################################################
#############################################################################
#
if ( ${new_mrc_created} == 'y' ) then
  #
  if ( ${imageorigin} == '4' ) then
    #############################################################################
    #                                                                           #
    ${proc_2dx}/linblock "LABEL - to reduce pixel amplitude by a factor of 4."
    ${proc_2dx}/linblock "LABEL - to reduce pixel amplitude by a factor of 4." >> History.dat
    #                                                                           #
    #############################################################################  
    #
    \mv -f ${imagename}.mrc SCRATCH/${imagename}.tmp.mrc
    #
    ${bin_2dx}/labelh.exe << eot
SCRATCH/${imagename}.tmp.mrc
2               ! switch to REAL (floating point)
${imagename}.mrc
0.25,0
1
eot
    #
  endif
  #
  if ( ${imageorigin} == '5' ) then
    #############################################################################
    #                                                                           #
    ${proc_2dx}/linblock "LABEL - to produce MODE=1 INTEGER*2 image with autoscaling 0...32k."
    ${proc_2dx}/linblock "LABEL - to produce MODE=1 INTEGER*2 image with autoscaling 0...32k." >> History.dat
    #                                                                           #
    #############################################################################  
    #
    \mv -f ${imagename}.mrc SCRATCH/${imagename}.tmp.mrc
    #
    ${bin_2dx}/labelh.exe << eot
SCRATCH/${imagename}.tmp.mrc
16               ! switch to INTEGER*2 (MODE 1) with autoscaling 0É32000
${imagename}.mrc
eot
    #
  endif
  #
endif
#
#
#
#
# This is to make sure that 2dx_maskCrystal displays the three file names even before it has been run
if ( ! -e LOGS/2dx_maskCrystal.results ) then
  echo "# IMAGE-IMPORTANT: ${nonmaskimagename}.mrc <Non-Masked Image>" >> LOGS/2dx_maskCrystal.results
  echo "# IMAGE-IMPORTANT: ManualMasking-CCmap.mrc <XCF map for Manual Masking>" >> LOGS/2dx_maskCrystal.results
  echo "# IMAGE-IMPORTANT: ManualMasking-UnbendPlot.mrc <Unbending Plot for Manual Masking>" >> LOGS/2dx_maskCrystal.results
endif
#
#############################################################################
echo ":: "
${proc_2dx}/linblock "Program Files and Parameters initialized."
echo ":: "
#############################################################################
#
echo "<<@progress: 100>>"
#
